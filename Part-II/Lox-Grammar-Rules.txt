STATEMENTS:
------------
program     -> declaration* EOF ;

declaration -> varDecl | statement ;

varDecl     -> "var" IDENTIFIER ( "=" expression )? ";" ;

statement   -> exprStmt | printStmt | ifStmt | block ;

exprStmt    -> expression ";" ;

printStmt   -> "print" expression ";" ;

ifStmt      -> "if" "(" expression ")" statement
               ( "else" statement )? ;

block       -> "{" declaration "}" ;



EXPRESSIONS:
------------
expression  -> assignment ( "," assignment )* ;

assignment  -> IDENTIFIER "=" assignment
               | ternary ;

ternary     -> equality ( "?" expression ":" expression )? ;

equality    -> comparison ( ( "!=" | "==" ) comparison )* ;

comparison  -> term ( ( ">" | ">=" | "<" | "<=" ) term )* ;

term        -> factor ( ( "-" | "+" ) factor )* ;

factor      -> unary ( ( "/" | "*" ) unary )*;

unary       -> ( "!" | "-" ) unary
               | primary ;

primary     -> NUMBER | STRING | "true" | "false" | "nil"
               | "(" expression ")" | IDENTIFIER ;
